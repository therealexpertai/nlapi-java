buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:7.4.1'
    }
}

plugins {
    id "application"
    id 'java'
    id 'maven-publish'
}

group = 'expert.ai'
version = '2.7.0'
description = 'Natural Language API expert.ai Java SDK v2'

publishing {
    publications {
        publish(MavenPublication) {
            from components.java
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    annotationProcessor("org.projectlombok:lombok:1.18.24")
    implementation('com.fasterxml.jackson.core:jackson-databind:2.14.1')
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.1')
    implementation('com.konghq:unirest-java:3.14.1')
    implementation('com.konghq:unirest-objectmapper-jackson:3.14.1')
    implementation ('org.slf4j:slf4j-api:2.0.5')
    compileOnly('org.projectlombok:lombok:1.18.24')
    testImplementation('junit:junit:4.13.2')
    implementation 'com.auth0:java-jwt:4.2.1'
}
apply plugin: 'org.owasp.dependencycheck'
test {
    useJUnit()
    minHeapSize = "512m"
    maxHeapSize = "1G"
    failFast = true
    reports.html.enabled = true
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
    filter {
        //include basic tests
        //includeTestsMatching "Local*"
        //excludeTestsMatching "Edge*"
        //excludeTestsMatching "*CredentialsProvider*"
        //include release tests
        includeTestsMatching "*ReleaseTest"
    }
}

java {
    sourceCompatibility = JavaVersion.toVersion('1.8')
    targetCompatibility = JavaVersion.toVersion('1.8')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task copyLicence(type: Copy) {
    from "${projectDir}/LICENSE"
    into "${buildDir}/resources/main/META-INF"
}

processResources.dependsOn copyLicence

jar {
    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
    }
}